buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.com.github.spotbugs.snom:spotbugs-gradle-plugin:4.5.1"
    }
}

plugins {
    // central reports in ./build/buildDashboard
    id 'build-dashboard'
}

subprojects {
    // Java plugin for dev
    apply plugin: 'java'

    // Groovy plugin for Spock tests
    apply plugin: 'groovy'

    //Jacoco
    apply plugin: 'jacoco'

    //pmd
    apply plugin: 'pmd'

    //checkstyle
    apply plugin: 'checkstyle'

    //stopbugs
    // apply plugin: 'com.github.spotbugs'

    group 'avenelt'
    version '1.0-SNAPSHOT'

    // build for Java 8
    sourceCompatibility = 1.8

    if (project.parent.name == 'clients' || project.name == 'server') {
        // apply the Java 'application' plugin - adds the 'run' task to run Java's main methods
        apply plugin: 'application'
    }

    // use Maven central to get libraries
    repositories {
        mavenCentral()
    }

    // add dependecy on 'common' project for all other projects
    if (project.name != 'common') {
        dependencies {
            implementation project(':common')
        }
    }

    // common dependencies for all projects
    dependencies {
        // add JUnit tests
        testImplementation group: 'junit', name: 'junit', version: '4.12'
    }

    test {
		afterTest { desc, result ->
			logger.quiet "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
		}
    }

    tasks.withType(Checkstyle) {
        reports {
            xml.enabled false
            html.enabled true
        }
    }

    pmd {
        ruleSets = ["/config/pmd/pmd-exclude.xml"]
    }
}

